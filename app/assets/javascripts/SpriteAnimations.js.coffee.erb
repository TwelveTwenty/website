class SpriteAnimations
	constructor: (@asset) ->
		@json = @asset.data('json')
		@rollover = @asset.data('rollover')
		@autostart = @asset.data('autostart')
		@canvas = ($ "<canvas></canvas>").insertBefore(@asset)
		@asset.load => @imageLoaded() if @asset.length > 0
		@asset.hide()

	imageLoaded: ->
		width = @asset.outerWidth()
		height = @asset.outerHeight()

		@canvas.attr('width',width)
		@canvas.attr('height',height)

		@stage = new Stage @canvas.get(0)

		$.ajax
			url: @json,
			dataType: 'json',
			success: @jsonLoaded

	jsonLoaded: (data) =>
		Ticker.addListener @stage

		@spriteSheet = new SpriteSheet data

		@assetAnimation = new BitmapAnimation @spriteSheet
		@assetAnimation.x = @width
		@stage.addChild @assetAnimation

		if @autostart
			@assetAnimation.play()
		else
			@assetAnimation.gotoAndStop(@assetAnimation.spriteSheet._numFrames - 1)

		@assetAnimation.onAnimationEnd = =>
			@assetAnimation.gotoAndStop(@assetAnimation.spriteSheet._numFrames - 1)

		if @rollover
			@canvas.mouseenter (e) =>
				@assetAnimation.gotoAndPlay(0)

($ document).ready ->
	Ticker.setFPS 30
	new SpriteAnimations ($ asset) for asset in ($ "[data-animate='true']")
